apply plugin: 'com.android.application'

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.1"

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    defaultConfig {
        applicationId "com.example.plugintest2"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 1
        versionName "1.0.0"
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
        textReport false
        htmlReport false
        xmlReport false
    }

    buildTypes {
        release {
            versionNameSuffix '_' + getDate()
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            versionNameSuffix '_' + getDate()
        }
    }

}

def getDate() {
    def date = new Date()
    def formattedDate = date.format('yyyy_MM_dd_HH_mm_ss')
    return formattedDate
}

configurations {
    baselinePatch
    baselineAp
    baselineObfuscate
}

dependencies {

    implementation 'com.facebook.rebound:rebound:0.3.5'

    //////////////////////////////////////////////////////////////////////
    //下面这一堆provided的配置，是因为这些包在编译宿主的时候，已经被打包到宿主的apk中。
    //所以这里打包非独立插件的时候，要通过provided配置防止这些包被打入非独立插件。
    //如果是独立插件，无需任何特殊配置
    /////////////////////////////////////////////////////////////////////

    compileOnly files(project(':Samples:PluginMain').getBuildDir().absolutePath + '/outputs/PluginMain-debug.jar')

    baselinePatch files(project(':Samples:PluginMain').getBuildDir().absolutePath + '/distributions/host.bar')
    baselineAp files(project(':Samples:PluginMain').getBuildDir().absolutePath + "/outputs/apk/debug/PluginMain-universal-debug.apk")

}

afterEvaluate {
    tasks.getByName("packageDebug").doLast {
        copy {
            println "复制插件" + apkPathList.get(0).absolutePath + "到宿主assets目录"
            from apkPathList.get(0).getParent()
            include project.name + '-debug.apk'
            into(project(':Samples:PluginMain').getProjectDir().absolutePath + '/src/main/assets/')
        }
    }
}

apply from: '/Users/cailiming/develop/Android-Plugin-Framework/FairyPlugin/plugin.gradle'
//apply from: "https://raw.githubusercontent.com/limpoxe/Android-Plugin-Framework/master/FairyPlugin/plugin.gradle"